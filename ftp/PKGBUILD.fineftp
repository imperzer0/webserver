#!/bin/bash
# Maintained by imper <imperator999mcpe@gmail.com>

pkgname=fineftp-server
pkgver=1.3.5
pkgrel=2
pkgdesc="FineFTP is a minimal FTP server library for Windows and Unix flavors"
arch=('any')
url="https://github.com/eclipse-ecal/fineftp-server"
license=('MIT')
depends=('asio')
makedepends=()
optdepends=()
source=(
  "https://github.com/eclipse-ecal/fineftp-server/archive/v$pkgver.tar.gz"
  "Findasio.cmake.patch"
  "custom_event_handler.hpp"
  "ftp_event_handler.h"
  "ftp_session.cpp.patch"
)
sha512sums=(
  '4a1e64389c8757385ba5bac81f4a0b1a8b1fc9886bdf80d9a2ff43f950ddb54389f3dd8499996f2462ed1fb3a656622ecec7127a30e6d3976120f3d1ef6c5948'
  '5a157af2c9cf573c2649ffecc99edba86383985c5adaba2ad318098c2709e907147c8ce5c359d423b04e60f237e37c3f81d59daaa0a4a7146245e668aa801865'
  '74fd48b6745d9f0c65b2c1906a4a6f4f826f30ce3521a7f51a9004461613625b15106900fc49c706ea8715dd61082aa409f66fa6ad3ca0bb9e3536175c3232a8'
  'e6cb6170be6125045869cfde4c5a2d7250bbcee0f20c2b70c3abfc29e6b8c6b040af0476d77783e9c88ae97feef6c367439e61a7501c34e48831aefaa4a6e8e0'
  'd2b4ee65af0cefacec0558943102276730fd14db4cfd0095579809aaa3136dd3e7dbb634ff7847bad09ee205c06d0b6b64b0d9345155eb8619960e1f8d3b7636'
)

prepare() {
  patch --forward --strip=1 --input="Findasio.cmake.patch" "$pkgname-$pkgver/cmake/Findasio.cmake"
  patch --forward --strip=1 --input="ftp_session.cpp.patch" "$pkgname-$pkgver/fineftp-server/src/ftp_session.cpp"
  cp -fv "custom_event_handler.hpp" "$pkgname-$pkgver/fineftp-server/src/"
  cp -fv "ftp_event_handler.h" "$pkgname-$pkgver/fineftp-server/src/"
}

build() {
  cd $pkgname-$pkgver
  mkdir -p _build
  cd _build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make -j 8
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cd _build
  DESTDIR="$pkgdir" make install
}
